def childProjects = rootProject.getChildProjects()

def applicationId = ""

def application = "com.android.application"
def library = "com.android.library"

childProjects.each {
    def projectObj = it.value
    def isApp = projectObj.plugins.hasPlugin(application)
    def isLib = projectObj.plugins.hasPlugin(library)

    if (isApp) {
        def applicationPlugin = projectObj.plugins.findPlugin(application)
        //自动获取 主工程的 sdk配置信息
        applicationId = applicationPlugin.extension.defaultConfig.applicationId
    }
}

if (!project.hasProperty("jpush_key")) {
    throw new IllegalStateException("请在[gradle.properties]中配置[jpush_key].")
}

def key = project.ext.jpush_key

if (project.plugins.hasPlugin(library)) {
    //只在lib中输出log
    println("极光key[jpush_key]:$key")
}

android {
    defaultConfig {

        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : key, //JPush 上注册的包名对应的 Appkey.
                JPUSH_CHANNEL: "developer-default", //暂时填写默认值即可.
        ]
    }
}